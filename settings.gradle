pluginManagement {
    repositories {
        gradlePluginPortal()
        maven {
            name = 'MinecraftForge'
            url = 'https://maven.minecraftforge.net/'
        }

        maven { url = 'https://maven.neoforged.net/releases' }
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.7.0'
}

rootProject.name = 'EventTransactionLib'

setupAllModuleFrom(file("modules"))
setupAllModuleFrom(file("runtime"))

def setupModule(String moduleName, String dir = "modules/${moduleName}") {
    include(moduleName)
    findProject(":${moduleName}")?.with {
        it.name = moduleName
        it.projectDir = new File(rootProject.projectDir, dir)
    }
}

def setupAllModuleFrom(File directory) {
    if (!directory.exists()) {
        return
    }
    if (!directory.isDirectory()) {
        throw new IllegalAccessException("Target directory is file")
    }
    for (File file : directory.listFiles()) {
        if (!file.isDirectory()) {
            System.err.println("Non-directory file ${file.name} detected - Skipping.")
            continue
        }
        setupModule(file.name, "${directory.name}/${file.name}")
    }
}

